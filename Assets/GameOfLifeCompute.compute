RWStructuredBuffer<uint> gridOut;
StructuredBuffer<uint> gridIn;
uint rng_state;
uint width;
uint gridWidth;
uint height;

uint getBit(uint input, uint pos) {
  return (input >> (pos)) & 1;
}

void setBit(inout uint input, uint pos) {
  input |= (1 << pos);
}

void clearBit(inout uint input, uint pos) {
  input &= ~(1 << pos);
}

#pragma kernel Update // ID 0
[numthreads(8, 8, 1)]
void Update(uint3 id : SV_DispatchThreadID) {
  // Leave 1 pixel border
  if (id.x == 0 || id.x >= gridWidth - 1 || id.y == 0 || id.y >= height - 1) {
    return;
  }

  uint index = id.y * gridWidth + id.x;

  // Get neighbouring indexes
  uint topLeft = gridIn[index - gridWidth - 1];
  uint top = gridIn[index - gridWidth];
  uint topRight = gridIn[index - gridWidth + 1];
  uint left = gridIn[index - 1];
  uint cell = gridIn[index];
  uint right = gridIn[index + 1];
  uint bottomLeft = gridIn[index + gridWidth - 1];
  uint bottom = gridIn[index + gridWidth];
  uint bottomRight = gridIn[index + gridWidth + 1];
  
  uint output = 0;

  // Leftmost cell (Bit 0)
  uint neighbours = getBit(topLeft, 31);
  neighbours += getBit(top, 0);
  neighbours += getBit(top, 1);
  neighbours += getBit(left, 31);
  neighbours += getBit(cell, 1);
  neighbours += getBit(bottomLeft, 31);
  neighbours += getBit(bottom, 0);
  neighbours += getBit(bottom, 1);
  
  uint alive = getBit(cell, 0);
  if ((alive && neighbours == 2) || neighbours == 3) {
    setBit(output, 0);
  } else {
    clearBit(output, 0);
  }

  // All middle cells (Bits 1-30)
  [unroll]
  for (int i = 1; i < 31; i++) {
    neighbours = getBit(top, i - 1);
    neighbours += getBit(top, i);
    neighbours += getBit(top, i + 1);
    neighbours += getBit(cell, i - 1);
    neighbours += getBit(cell, i + 1);
    neighbours += getBit(bottom, i - 1);
    neighbours += getBit(bottom, i);
    neighbours += getBit(bottom, i + 1);

    alive = getBit(cell, i);
    if ((alive && neighbours == 2) || neighbours == 3) {
      setBit(output, i);
    } else {
      clearBit(output, i);
    }
  }

  // Rightmost cell (Bit 31)
  neighbours = getBit(top, 30);
  neighbours += getBit(top, 31);
  neighbours += getBit(topRight, 0);
  neighbours += getBit(cell, 30);
  neighbours += getBit(right, 0);
  neighbours += getBit(bottom, 30);
  neighbours += getBit(bottom, 31);
  neighbours += getBit(bottomRight, 0);

  alive = getBit(cell, 31);
  if ((alive && neighbours == 2) || neighbours == 3) {
    setBit(output, 31);
  } else {
    clearBit(output, 31);
  }

  gridOut[index] = output;
}

uint pcg_hash(uint input) {
  uint state = input * 747796405u + 2891336453u;
  uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
  return ((word >> 22u) ^ word);
}

#pragma kernel Generate // ID 1
[numthreads(8, 8, 1)]
void Generate(uint3 id : SV_DispatchThreadID) {
  // Leave 1 pixel border
  if (id.x == 0 || id.x >= gridWidth - 1 || id.y == 0 || id.y >= height - 1) {
    return;
  }

  uint index = id.y * gridWidth + id.x;
  gridOut[index] = pcg_hash(rng_state + index);
}

#pragma kernel Clear // ID 2
[numthreads(8, 8, 1)]
void Clear(uint3 id : SV_DispatchThreadID) {
  int index = id.y * gridWidth + id.x;
  gridOut[index] = 0;
}

float2 mousePos;
bool erase;
#pragma kernel Draw // ID 3
[numthreads(1, 1, 1)]
void Draw(uint3 id : SV_DispatchThreadID) {
  uint yPos = (mousePos.y * 64) + (height / 2);
  uint xPos = (mousePos.x * 64) + (width / 2);

  // Don't allow drawing out of bounds
  if (xPos < 32 || xPos >= width - 32 || yPos == 0 || yPos >= height - 1) {
    return;
  }

  uint gridXPos = (mousePos.x * 2) + (width / 64.0);
  uint bitPos = (yPos * width + xPos) % 32;
  uint gridIndex = yPos * gridWidth + gridXPos;
  
  if (erase) {
    clearBit(gridOut[gridIndex], bitPos);
  } else {
    setBit(gridOut[gridIndex], bitPos);
  }
}